plugins {
    id 'fabric-loom' version '1.2-SNAPSHOT'
    id 'idea'
    id 'io.github.CDAGaming.cursegradle' version '1.+'
    id "com.modrinth.minotaur" version "2.+"
}

archivesBaseName = "${mod_id}-fabric"

repositories {
    maven { url "https://maven.shedaniel.me/" }
    maven {
        url = "https://maven.modmuss50.me/"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
//    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    modImplementation "me.zeroeightsix:fiber:${fiber_version}"
    include "me.zeroeightsix:fiber:${fiber_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation project(":Common")
}

loom {
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}


processResources {
    from project(":Common").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

jar {
    from("LICENSE") {
        rename { "${it}_${mod_name}" }
    }
}

//publishing {
//    publications {
//        mavenJava(MavenPublication) {
//            groupId project.group
//            artifactId project.archivesBaseName
//            version project.version
//            from components.java
//        }
//    }
//
//    repositories {
//        maven {
//            url "file://" + System.getenv("local_maven")
//        }
//    }
//}

def ENV = System.getenv()
def release_title = "[Fabric] v${version}"

if (ENV.CF_TOKEN) {
    curseforge {
        apiKey = ENV.CF_TOKEN
        project {
            id = curse_id
            releaseType = release_type
            addGameVersion "Fabric"
            addGameVersion "Quilt"
            addGameVersion "Java 17"
            addGameVersion "Server"
            addGameVersion "Client"
            addGameVersion minecraft_version
            addGameVersion "1.20.1"
            mainArtifact(remapJar.archivePath) {
                it.displayName = release_title
            }
            changelog = rootProject.file('changelog.md')
            changelogType = 'markdown'

        }

        options {
            forgeGradleIntegration = false
        }
    }
}

if (ENV.MODRINTH_TOKEN) {
    modrinth {
        token = ENV.MODRINTH_TOKEN
        projectId = modrinth_id
        versionType = release_type
        versionName = release_title
        versionNumber = "${version}+fabric"
        uploadFile = remapJar
        gameVersions = [ minecraft_version, "1.20.1" ]
        loaders = ["fabric", "quilt"]
        changelog = rootProject.file('changelog.md').text
    }
}
